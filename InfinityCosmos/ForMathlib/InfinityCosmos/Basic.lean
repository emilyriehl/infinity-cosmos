import InfinityCosmos.ForMathlib.AlgebraicTopology.SimplicialCategory.Basic
import InfinityCosmos.ForMathlib.AlgebraicTopology.SimplicialCategory.Cotensors
import InfinityCosmos.ForMathlib.AlgebraicTopology.SimplicialCategory.Limits
import InfinityCosmos.ForMathlib.AlgebraicTopology.SimplicialSet.MorphismProperty
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

namespace CategoryTheory
open Category Limits Functor MonoidalCategory Simplicial SimplicialCategory SSet
universe w v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

variable (K : Type u) [Category.{v} K] [SimplicialCategory K]

/-- A `PreInfinityCosmos` is a simplicially enriched category whose hom-spaces are quasi-categories
and whose morphisms come equipped with a special class of isofibrations.-/
class PreInfinityCosmos extends SimplicialCategory K where
  [has_qcat_homs : ‚àÄ {X Y : K}, SSet.Quasicategory (EnrichedCategory.Hom X Y)]
  IsIsofibration : MorphismProperty K

namespace InfinityCosmos

variable {K : Type u} [Category.{v} K] [PreInfinityCosmos.{v} K]

open PreInfinityCosmos

/-- Common notation for the hom-spaces in a pre-‚àû-cosmos.-/
def Fun (A B : K) : QCat where
  obj := EnrichedCategory.Hom A B
  property := PreInfinityCosmos.has_qcat_homs (X := A) (Y := B)

noncomputable def representableMap' {X A B : K} (f : ùüô_ SSet ‚ü∂ EnrichedCategory.Hom A B) :
    (EnrichedCategory.Hom X A : SSet) ‚ü∂ (EnrichedCategory.Hom X B) :=
  (œÅ_ _).inv ‚â´ _ ‚óÅ f ‚â´ EnrichedCategory.comp (V := SSet) X A B

noncomputable def representableMap (X : K) {A B : K} (f : A ‚ü∂ B) :
    (EnrichedCategory.Hom X A : SSet) ‚ü∂ (EnrichedCategory.Hom X B) :=
  representableMap' (eHomEquiv SSet f)

noncomputable def toFunMap (X : K) {A B : K} (f : A ‚ü∂ B) :
    Fun X A ‚ü∂ Fun X B := representableMap X f

-- Arguments of this type have the form `‚ü® f hf ‚ü©`
def Isofibration (X Y : K) : Type v := {f : X ‚ü∂ Y // IsIsofibration f}

-- Type with "\rr".
infixr:25  " ‚Ü† " => Isofibration

instance (A B : K) : Coe (A ‚Ü† B) (A ‚ü∂ B) := ‚ü® Œª f ‚Ü¶ f.1 ‚ü©

end InfinityCosmos

open PreInfinityCosmos InfinityCosmos
variable (K : Type u) [Category.{v} K][PreInfinityCosmos.{v} K]

/-- Experimenting with some changes.-/
class InfinityCosmos extends PreInfinityCosmos K where
  comp_isIsofibration {X Y Z : K} (f : X ‚Ü† Y) (g : Y ‚Ü† Z) : IsIsofibration (f.1 ‚â´ g.1)
  iso_isIsofibration {X Y : K} (e : X ‚ü∂ Y) [IsIso e] : IsIsofibration e
  all_objects_fibrant {X Y : K} (hY : IsTerminal Y) (f : X ‚ü∂ Y) : IsIsofibration f
        -- TODO: replace by IsConicalTerminal?
  [has_products : HasConicalProducts K]
  prod_map_fibrant {Œ≥ : Type w} {A B : Œ≥ ‚Üí K} (f : ‚àÄ i, A i ‚Ü† B i) :
    IsIsofibration (Limits.Pi.map (Œª i ‚Ü¶ (f i).1))
  [has_isoFibration_pullbacks {A B E : K} (f : A ‚ü∂ B) (g : E ‚Ü† B) : HasConicalPullback f g.1]
  pullback_is_isoFibration {A B E P : K} (f : A ‚ü∂ B) (g : E ‚Ü† B)
    (fst : P ‚ü∂ A) (snd : P ‚ü∂ E) (h : IsPullback fst snd f g.1) : IsIsofibration fst
  [has_limits_of_towers (F : ‚Ñï·µí·µñ ‚•§ K) :
    (‚àÄ n : ‚Ñï, IsIsofibration (F.map (homOfLE (Nat.le_succ n)).op)) ‚Üí HasConicalLimit F]
  has_limits_of_towers_isIsofibration (F : ‚Ñï·µí·µñ ‚•§ K) (hf) :
    haveI := has_limits_of_towers F hf
    IsIsofibration (limit.œÄ F (.op 0))
  [has_cotensors : HasCotensors K]
  leibniz_cotensor  {U V : SSet} (i : U ‚ü∂ V) [Mono i] {A B : K} (f : A ‚Ü† B) {P : K}
    (fst : P ‚ü∂ V ‚ãî B) (snd : P ‚ü∂ U ‚ãî A)
    (h : IsPullback fst snd (cotensorContraMap i B) (cotensorCovMap U f)) :
    IsIsofibration (h.isLimit.lift <|
      PullbackCone.mk (cotensorCovMap V f.1) (cotensorContraMap i A)
        (cotensor_bifunctoriality i f.1)) --TODO : Prove that these pullbacks exist.
  local_isoFibration {X A B : K} (f : A ‚Ü† B) : Isofibration (toFunMap X f.1)


attribute [instance] has_products has_isoFibration_pullbacks has_limits_of_towers has_cotensors

namespace InfinityCosmos

variable {K : Type u} [Category.{v} K] [InfinityCosmos K]

open InfinityCosmos PreInfinityCosmos

instance : HasConicalTerminal K := by infer_instance

instance : HasCotensors K := by infer_instance

instance : HasTerminal K := by infer_instance

instance : HasProducts K := by infer_instance

theorem binary_prod_map_fibrant {X Y X' Y' : K} {f : X ‚Ü† Y} {g : X' ‚Ü† Y'} :
    IsIsofibration (prod.map f.1 g.1) := sorry

def compIsofibration {A B C : K} (f : A ‚Ü† B) (g : B ‚Ü† C) : A ‚Ü† C :=
  ‚ü®(f.1 ‚â´ g.1), comp_isIsofibration f g‚ü©

section pullbacks

lemma initialSquare_isPullback' (U : SSet.{v}) (B : K) :
    IsPullback  (cotensorContraMap (initial.to U) B) (ùüô _)
    (ùüô _) (cotensorContraMap (initial.to U) B) := IsPullback.of_id_snd

-- TODO: replace `cotensor.iso.underlying` with something for general cotensor API.
noncomputable def cotensorInitialIso (A : K) : (‚ä•_ SSet ) ‚ãî A ‚âÖ ‚ä§_ K where
  hom := terminal.from ((‚ä•_ SSet) ‚ãî A)
  inv := (cotensor.iso.underlying (‚ä•_ SSet) A (‚ä§_ K)).symm (initial.to (sHom (‚ä§_ K) A))
  hom_inv_id := (cotensor.iso.underlying (‚ä•_ SSet) A ((‚ä•_ SSet ) ‚ãî A)).injective
    (initial.hom_ext _ _)
  inv_hom_id := terminal.hom_ext _ _

noncomputable instance cotensorInitial_isTerminal (A : K) : IsTerminal ((‚ä•_ SSet ) ‚ãî A) :=
  terminalIsTerminal.ofIso (id (cotensorInitialIso A).symm)

noncomputable def initialSquare.snd (U : SSet.{v}) (A B : K) : U ‚ãî B ‚ü∂ (‚ä•_ SSet ) ‚ãî A :=
  terminal.from (U ‚ãî B) ‚â´ (cotensorInitialIso A).inv

lemma initialSquare_isIso {A B : K} (f : A ‚ü∂ B) : IsIso (cotensorCovMap (‚ä•_ SSet) f) :=
  isIso_of_isTerminal (cotensorInitial_isTerminal A) (cotensorInitial_isTerminal B)
    (cotensorCovMap (‚ä•_ SSet) f)

lemma initialSquare_isPullback (U : SSet.{v}) {A B : K} (f : A ‚Ü† B) :
    IsPullback (ùüô _) (initialSquare.snd U A B)
      (cotensorContraMap (initial.to U) B) (cotensorCovMap (‚ä•_ SSet) f.1) := by
  have := initialSquare_isIso f.1
  refine IsPullback.of_horiz_isIso ?_
  unfold initialSquare.snd
  constructor
  apply IsTerminal.hom_ext (cotensorInitial_isTerminal _)

theorem cotensorCovMap_fibrant (U : SSet.{v}) {A B : K} (f : A ‚Ü† B) :
    IsIsofibration (cotensorCovMap U f.1) := by
  let map : ‚ä•_ SSet ‚ü∂ U := initial.to U
  have hyp : Mono map := Initial.mono_to U
  have := leibniz_cotensor (initial.to U) f _ _ (initialSquare_isPullback U f)
  have := IsPullback.lift_fst (initialSquare_isPullback U f) (cotensorCovMap U f.1)
    (cotensorContraMap (initial.to U) A) (cotensor_bifunctoriality (initial.to U) f.1)
  simp only [comp_id] at this
  rw [‚Üê this]
  exact (leibniz_cotensor (initial.to U) f _ _ (initialSquare_isPullback U f))

end pullbacks

end InfinityCosmos

end CategoryTheory
