/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import InfinityCosmos.Mathlib.AlgebraicTopology.SimplicialSet.Monoidal
import Mathlib.CategoryTheory.Enriched.Basic
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Closed.FunctorToTypes

/-!
# Simplicial categories

A simplicial category is a category `C` that is enriched over the
category of simplicial sets in such a way that morphisms in
`C` identify to the `0`-simplices of the enriched hom.

## TODO

* construct a simplicial category structure on simplicial objects, so
that it applies in particular to simplicial sets
* obtain the adjunction property `(K ‚äó X ‚ü∂ Y) ‚âÉ (K ‚ü∂ sHom X Y)` when `K`, `X`, and `Y`
are simplicial sets
* develop the notion of "simplicial tensor" `K ‚äó‚Çõ X : C` with `K : SSet` and `X : C`
an object in a simplicial category `C`
* define the notion of path between `0`-simplices of simplicial sets
* deduce the notion of homotopy between morphisms in a simplicial category
* obtain that homotopies in simplicial categories can be interpreted as given
by morphisms `Œî[1] ‚äó X ‚ü∂ Y`.

## References
* [Daniel G. Quillen, *Homotopical algebra*, II ¬ß1][quillen-1967]

-/

universe v u

open CategoryTheory Category MonoidalCategory Simplicial SSet

namespace CategoryTheory

variable (C : Type u) [Category.{v} C]

/-- A simplicial category is a category `C` that is enriched over the
category of simplicial sets in such a way that morphisms in
`C` identify to the `0`-simplices of the enriched hom. -/
class SimplicialCategory extends EnrichedCategory SSet.{v} C where
  /-- morphisms identify to `0`-simplices of the enriched hom -/
  homEquiv (K L : C) : (K ‚ü∂ L) ‚âÉ (ùüô_ SSet.{v} ‚ü∂ EnrichedCategory.Hom K L)
  homEquiv_id (K : C) : homEquiv K K (ùüô K) = eId SSet K := by aesop_cat
  homEquiv_comp {K L M : C} (f : K ‚ü∂ L) (g : L ‚ü∂ M) :
    homEquiv K M (f ‚â´ g) = (Œª_ _).inv ‚â´ (homEquiv K L f ‚äó homEquiv L M g) ‚â´
      eComp SSet K L M := by aesop_cat

namespace SimplicialCategory

variable [SimplicialCategory C]

variable {C}

/-- Abbreviation for the enriched hom of a simplicial category. -/
abbrev sHom (K L : C) : SSet.{v} := EnrichedCategory.Hom K L

/-- Abbreviation for the enriched composition in a simplicial category. -/
abbrev sHomComp (K L M : C) : sHom K L ‚äó sHom L M ‚ü∂ sHom K M := eComp SSet K L M

/-- The bijection `(K ‚ü∂ L) ‚âÉ sHom K L _[0]` for all objects `K` and `L`
in a simplicial category. -/
def homEquiv' (K L : C) : (K ‚ü∂ L) ‚âÉ sHom K L _[0] :=
  (homEquiv K L).trans (sHom K L).unitHomEquiv

/-- The morphism `sHom K' L ‚ü∂ sHom K L` induced by a morphism `K ‚ü∂ K'`. -/
noncomputable def sHomWhiskerRight {K K' : C} (f : K ‚ü∂ K') (L : C) :
    sHom K' L ‚ü∂ sHom K L :=
  (Œª_ _).inv ‚â´ homEquiv K K' f ‚ñ∑ _ ‚â´ sHomComp K K' L

@[simp]
lemma sHomWhiskerRight_id (K L : C) : sHomWhiskerRight (ùüô K) L = ùüô _ := by
  simp [sHomWhiskerRight, homEquiv_id]

@[simp, reassoc]
lemma sHomWhiskerRight_comp {K K' K'' : C} (f : K ‚ü∂ K') (f' : K' ‚ü∂ K'') (L : C) :
    sHomWhiskerRight (f ‚â´ f') L = sHomWhiskerRight f' L ‚â´ sHomWhiskerRight f L := by
  dsimp [sHomWhiskerRight]
  simp only [assoc, homEquiv_comp, comp_whiskerRight, leftUnitor_inv_whiskerRight, ‚Üê e_assoc']
  rfl

/-- The morphism `sHom K L ‚ü∂ sHom K L'` induced by a morphism `L ‚ü∂ L'`. -/
noncomputable def sHomWhiskerLeft (K : C) {L L' : C} (g : L ‚ü∂ L') :
    sHom K L ‚ü∂ sHom K L' :=
  (œÅ_ _).inv ‚â´ _ ‚óÅ homEquiv L L' g ‚â´ sHomComp K L L'

@[simp]
lemma sHomWhiskerLeft_id (K L : C) : sHomWhiskerLeft K (ùüô L) = ùüô _ := by
  simp [sHomWhiskerLeft, homEquiv_id]

@[simp, reassoc]
lemma sHomWhiskerLeft_comp (K : C) {L L' L'' : C} (g : L ‚ü∂ L') (g' : L' ‚ü∂ L'') :
    sHomWhiskerLeft K (g ‚â´ g') = sHomWhiskerLeft K g ‚â´ sHomWhiskerLeft K g' := by
  dsimp [sHomWhiskerLeft]
  simp only [homEquiv_comp, MonoidalCategory.whiskerLeft_comp, assoc, ‚Üê e_assoc]
  rfl

@[reassoc]
lemma sHom_whisker_exchange {K K' L L' : C} (f : K ‚ü∂ K') (g : L ‚ü∂ L') :
    sHomWhiskerLeft K' g ‚â´ sHomWhiskerRight f L' =
      sHomWhiskerRight f L ‚â´ sHomWhiskerLeft K g :=
  ((œÅ_ _).inv ‚â´ _ ‚óÅ homEquiv L L' g ‚â´ (Œª_ _).inv ‚â´ homEquiv K K' f ‚ñ∑ _) ‚â´=
    (e_assoc SSet.{v} K K' L L').symm

attribute [local simp] sHom_whisker_exchange

variable (C) in
/-- The bifunctor `C·µí·µñ ‚•§ C ‚•§ SSet.{v}` which sends `K : C·µí·µñ` and `L : C` to `sHom K.unop L`. -/
@[simps]
noncomputable def sHomFunctor : C·µí·µñ ‚•§ C ‚•§ SSet.{v} where
  obj K :=
    { obj := fun L => sHom K.unop L
      map := fun œÜ => sHomWhiskerLeft K.unop œÜ }
  map œÜ :=
    { app := fun L => sHomWhiskerRight œÜ.unop L }

noncomputable instance : SimplicialCategory SSet where
  toEnrichedCategory := inferInstanceAs (EnrichedCategory (_ ‚•§ Type _) (_ ‚•§ Type _))
  homEquiv K L :=
    letI e : (K ‚ü∂ L) ‚âÉ (K ‚äó ùüô_ SSet ‚ü∂ L) :=
      ‚ü®fun f => (œÅ_ _).hom ‚â´ f, fun f => (œÅ_ _).inv ‚â´ f, by aesop_cat, by aesop_cat‚ü©
    e.trans (Functor.homObjEquiv _ _ _).symm |>.trans (Functor.functorHomEquiv K L (ùüô_ SSet)).symm
  homEquiv_id := by aesop_cat
  homEquiv_comp := by aesop_cat

noncomputable instance : MonoidalClosed SSet where
  closed A := {
    rightAdj := (sHomFunctor _).obj ‚ü®A‚ü©
    adj := FunctorToTypes.adj _
  }

noncomputable instance : SymmetricCategory SSet := inferInstance
-- instance : HasBinaryProducts SSet := by infer_instance

/-- The monoidal structure given by the `ChosenFiniteProducts` has good definitional properties,
like the following: -/
example (R S : SSet) (n : SimplexCategory) : (R ‚äó S).obj ‚ü®n‚ü© = (R.obj ‚ü®n‚ü© √ó S.obj ‚ü®n‚ü©) := rfl

end SimplicialCategory

end CategoryTheory
